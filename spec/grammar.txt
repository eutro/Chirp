Program ::= ( Statement ( (','|'\n') Statement )* )?

Statement ::= Defn | Expr

Defn ::= 'defn' Binding

Binding ::= Identifier Arguments? TypeHint? '=' ( Expr | TForeign )

Arguments ::= ( '(' ( RawBinding ( ',' RawBinding )* )? ')' )

RawBinding ::= Identifier TypeHint?

Identifier ::= TIdent

TypeHint ::= ':' Type

Type ::= PlaceholderType | NamedType

PlaceholderType ::= '_'

NamedType ::= Identifier ( '<' Type ( ',' Type )* ','? '>' )?

Expr ::= LogCmpExpr

LogCmpExpr ::= EqExpr ( ('&&' | '||') EqExpr )*

EqExpr ::= CmpExpr ( ('!='|'='|'==') CmpExpr )*

CmpExpr ::= BitExpr ( ('<'|'<='|'>'|'>=') BitExpr )*

BitExpr ::= ShiftExpr ( ('|' | '&') ShiftExpr )*

ShiftExpr ::= AddExpr ( ('<<'|'>>') AddExpr )*

AddExpr ::= MulExpr ( ('+'|'-') MulExpr )*

MulExpr ::= HintedExpr ( ('*'|'/'|'%') HintedExpr )*

HintedExpr ::= PrefixExpr TypeHint?

PrefixExpr ::= ('+'|'-')* PostfixExpr

PostfixExpr ::= PrimaryExpr ( '(' ( Expr ( ',' Expr )* ','? )? ')' )*

PrimaryExpr ::= VarExpr | LiteralExpr | DelimitedExpr | IfExpr | LetExpr | FnExpr | LambdaExpr

VarExpr ::= Identifier

LiteralExpr ::= TStr | TInt | TFloat | TTrue | TFalse

DelimitedExpr ::= BlockExpr | BracketExpr

BlockExpr ::= '{' ( ( Statement (','|'\n') )* Expr )? '}'

BracketExpr ::= '(' Expr ')'

IfExpr ::= 'if' Expr BlockExpr
           ( 'else' 'if' Expr BlockExpr )*
           ( 'else' BlockExpr )?

LetExpr ::= 'let' ( Binding ( (','|'\n') Binding )* ','? )? 'in' Identifier? DelimitedExpr

FnExpr ::= 'fn' Identifier? Arguments TypeHint? '=' Expr

LambdaExpr ::= TBackslash ( Identifier ( ',' Identifier )* )? '.' Expr
