Program ::= Statement*

Statement ::= Defn | Expr

Defn ::= 'defn' Binding

Binding ::= Identifier ( '(' ( RawBinding ( ',' RawBinding )? )? ')' )? TypeHint? '=' Expr

RawBinding ::= Identifier TypeHint?

Identifier ::= TIdent

TypeHint ::= ':' Type

Type ::= PlaceholderType | NamedType

PlaceholderType ::= '_'

NamedType ::= Identifier ( '<' Type ( ',' Type )* ','? '>' )?

Expr ::= LogCmpExpr

LogCmpExpr ::= EqExpr ( ('&&' | '||') EqExpr )*

EqExpr ::= CmpExpr ( ('!='|'='|'==') CmpExpr )*

CmpExpr ::= BitExpr ( ('<'|'<='|'>'|'>=') BitExpr )*

BitExpr ::= ShiftExpr ( ('|' | '&') ShiftExpr )*

ShiftExpr ::= AddExpr ( ('<<'|'>>') AddExpr )*

AddExpr ::= MulExpr ( ('+'|'-') MulExpr )*

MulExpr ::= HintedExpr ( ('*'|'/'|'%') HintedExpr )*

HintedExpr ::= PrefixExpr TypeHint?

PrefixExpr ::= ('+'|'-')* PostfixExpr

PostfixExpr ::= PrimaryExpr | FunCalLExpr

FunCallExpr ::= PrimaryExpr ( '(' ( Expr ( ',' Expr )* ','? )? ')' )

PrimaryExpr ::= VarExpr | LiteralExpr | DelimitedExpr | IfExpr | LetExpr

VarExpr ::= Identifier

LiteralExpr ::= TStr | TInt | TFloat | TTrue | TFalse

DelimitedExpr ::= BlockExpr | BracketExpr

BlockExpr ::= '{' ( Statement (','|'\n') )* Expr '}'

BracketExpr ::= '(' Expr ')'

IfExpr ::= 'if' Expr BlockExpr
           ( 'else' 'if' Expr BlockExpr )*
           ( 'else' BlockExpr )?

LetExpr ::= 'let' ( Binding ( (','|'\n') Binding )* ','? )? 'in' Identifier? DelimitedExpr
