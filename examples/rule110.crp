// Implementation of the Rule 110 cellular automation,
// which is known to be Turing complete.

// Yes, this is just a lambda calculus implementation of Rule 110,
// but this is all handled by the type system at compile time.
// Note that there is a hard limit on max recursion depth, though.

defn tru(x, y) = x
defn fls(x, y) = y

defn cons(x, y) = λifpair,ifnull.ifpair(x, y)
defn nil = λifpair,ifnull.ifnull()

/*
Current pattern           111 110 101 100 011 010 001 000
New state for center cell  0   1   1   0   1   1   1   0
*/
defn stepabc(a, b, c) =
  a(b(c(fls, tru), c(tru, fls)), b(c(tru, tru), c(tru, fls)))

defn steplist(l, tail) =
  tail(
    λm,rtail.cons(
      stepabc(l,m,rtail(λr,i.r, λ.fls)),
      steplist(m, rtail)
    ),
    λ.nil
  )

defn step(list) =
  steplist(fls, cons(fls, list))

// io
defn putI(i: i64): #() = foreign
defn putS(s: string): #() = foreign
defn println(): #() = foreign
defn printB(b) = putI(b(1, 0))
defn printL(list) =
  list(λl,tail.{printB(l),printL(tail)}, println)
defn printAndStep(list) = {printL(list), step(list)}

defn nine = λf.λx.f(f(f(f(f(f(f(f(f(x)))))))))

putS("Static:\n")
printL(nine(printAndStep)(

// specify the pattern
cons(tru,
//

nil)))

defn convBool(x) = if x then tru else fls

putS("Dynamic:\n")
printL(nine(printAndStep)(

cons(convBool(true),

nil)))
